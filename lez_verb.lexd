
PATTERN Verb
(MasdarStem [<verb>:]  MasdarDerived)[^[Strong,Weak]]( Plural | Erg)?
(ImperfStem [<verb>:] (Infinitive | Hortative | Imperfective | Future | Prohibitive))[^[Strong,Weak]]
(ImperfStem [<verb>:] ImperfConverb)[^[Strong,Weak]]
(AoristStem [<verb>:] AoristConverb)[^[Strong,Weak]]
(AoristStem [<verb>:] Aorist)[^[Strong,Weak]]
(AoristStem [<verb>:] Perfect)[^[Strong,Weak]]
(AoristStem [<verb>:] Perfect SecondaryImperfectiveConverb)[^[Strong,Weak]]
(MasdarStem [<verb>:] MasdarDerived)[^[Strong,Weak]] Erg (MasdarInfl | (MasdarInflEssive (Elative | Directive)?))
(MasdarStem [<verb>:] Optative)[^[Strong,Weak]]
(AoristStem [<verb>:] (Aorist | Perfect)) | (ImperfStem [<verb>:] (Imperfective | Future))[^[Strong,Weak]] Negative
Imperative [<verb>:] [<imper>:]
(AoristStem (AoristParticiple | PerfParticiple))[^[Strong,Weak],^[Polysyllabic,Monosyllabic,Exception]] # атрибутивная форма
(AoristStem (AoristParticiple | PerfParticiple) AdjNumber [<abs>:])[^[Strong,Weak],^[Polysyllabic,Monosyllabic,Exception]] # субстантивированная
(AoristStem (AoristParticiple | PerfParticiple) AdjErg (Case | (InflEssive (Elative | Directive)?))?)[^[Strong,Weak],^[Polysyllabic,Monosyllabic,Exception]] # субстантивированная
(AoristStem AoristParticiple TempConverb)[^[Strong,Weak],^[Polysyllabic,Monosyllabic,Exception]]
(AoristStem PerfParticiple TempConverb)[^[Strong,Weak]]
(ImperfStem ImperfParticiple TempConverb)[^[Strong,Weak]]
(AoristStem AoristParticiple ImmediateAnteriorConverb)[^[Strong,Weak],^[Polysyllabic,Monosyllabic,Exception]]
(AoristStem AoristParticiple ConditionalMood)[^[Strong,Weak],^[Polysyllabic,Monosyllabic,Exception]]
(ImperfStem (Imperfective | Future) ConditionalMood)[^[Strong,Weak]]
(AoristStem (Aorist | Perfect) ConditionalMood)[^[Strong,Weak]]
(ImperfStem (Imperfective | Future) InterrogativeMood)[^[Strong,Weak]]
(AoristStem (Aorist | Perfect) InterrogativeMood)[^[Strong,Weak]]
(AoristStem AoristParticiple (MannerConverb | CasualConverb))[^[Strong,Weak],^[Polysyllabic,Monosyllabic,Exception]]
(ImperfStem ImperfParticiple (MannerConverb | CasualConverb))[^[Strong,Weak]]
(AoristStem PerfParticiple (MannerConverb | CasualConverb))[^[Strong,Weak]]
(ImperfStem ImperfParticiple)[^[Strong,Weak]] # атрибутивная форма
(ImperfStem ImperfParticiple AdjNumber [<abs>:])[^[Strong,Weak]]
(ImperfStem ImperfParticiple AdjErg (Case | (InflEssive (Elative | Directive)?))?)[^[Strong,Weak]]

LEXICON Plural
<pl>:ар

LEXICON Erg
<erg>:и
<pl><erg>:ри

LEXICON MasdarInfl
<gen>:н
<dat>:з

LEXICON MasdarInflEssive
<ad><essive>:в
<post><essive>:хъ
<sub><essive>:к
<in><essive>:а
<super><essive>:ал

LEXICON Elative
<elative>:ай

LEXICON Directive
<directive>:ди

LEXICON MasdarDerived
<msd>:н[Strong]
<msd>:ун[Weak]

LEXICON Optative
<opt>:рай
<opt>:й[Strong]
<opt>:уй[Weak]

LEXICON Infinitive # imperfective converb
<inf>:з[Strong]
<inf>:из[Weak]

LEXICON Imperfective
<imperf>:зва[Strong]
<imperf>:зава[Weak]
<contimperf>:зма[Strong]
<contimperf>:зама[Weak]

LEXICON ImperfConverb
<postr>:далди
<grad>:рдавай[Strong]
<grad>:ирдавай[Weak]
<immant>:замаз[Weak]
<immant>:змаз[Strong]

LEXICON Hortative
<hort>:н[Strong]
<hort>:ин[Weak]

LEXICON Future
<fut>:да # будущее

LEXICON Prohibitive
<prohib>:мир

LEXICON Aorist
<aor>:на # прошедшее определенное

LEXICON Perfect
<prf>:нва[Strong]
<prf>:нава[Weak]
<contprf>:нма[Strong]
<contprf>:нама[Weak]

LEXICON AoristConverb
<aoc>:на
<immant>:нмаз[Strong]
<immant>:нмазди[Strong]
<immant>:намаз[Weak]
<immant>:намазди[Weak]

LEXICON TempConverb
<temp>:ла

LEXICON Negative
<neg>:ч

LEXICON AoristParticiple
<aor><ptp>:ай[Weak]
<aor><ptp>:р[Strong,Exception]
<aor><ptp>:й[Strong,Polysyllabic]
<aor><ptp>:йи[Strong,Monosyllabic]

LEXICON ImperfParticiple
<imperf><ptp>:звай[Strong]
<imperf><ptp>:завай[Weak]
<fut><ptp>:дай

LEXICON PerfParticiple
<prf><ptp>:нвай[Strong]
<prf><ptp>:навай[Weak]

LEXICON ImmediateAnteriorConverb
<immant>:валди

LEXICON ConditionalMood
<cnd>:тӀа

LEXICON InterrogativeMood
<inter>:ни

LEXICON SecondaryImperfectiveConverb
<imc>:з

LEXICON MannerConverb
<man>:вал

LEXICON CasualConverb
<caus>:виляй 

