PATTERNS
(Stem [<n>:] (([<abs>] Plural?) | (Erg | ErgPlural)))[^[Decl1,Decl2,Decl3,Decl4,Decl5],^[Back,NonBack,Labial]]
(Stem [<n>:] (Erg | ErgPlural))[^[Decl1,Decl2,Decl3,Decl4,Decl5],^[Back,NonBack,Labial]] (NounInfl) | (NounInflEssive (Elative | Directive)?)

LEXICON Stem

багъ[Decl2,Back] # сад
ват[Decl3,Back] # катушка
викI[Decl3,NonBack] # ярмо
гав[Decl5,Back] # тур
гар[Decl1,Back] # ветер
дава[Decl2,Back] # лекарство
кицI[Decl1,NonBack] # собака
къум[Decl4,Back] # песок
лиф[Decl5,NonBack] # голубь
пел[Decl1,NonBack] # лоб
сал[Decl1,Back] # огород
тар[Decl2,Back] # дерево
туьд[Decl3,NonBack,Labial] # горло
фикир[Decl2,Back] # мысль
хьур[Decl1,NonBack] # село
чиг[Decl4,NonBack] # роса

LEXICON NounInfl
<gen>:н
<dat>:з

LEXICON Erg[Decl1]
<erg>:a[Back]
<erg>:е[NonBack]

LEXICON Erg[Decl2]
<erg>:ди

LEXICON Erg[Decl3]
<erg>:уни[Back]
<erg>:ини[NonBack]
<erg>:уьни[NonBack,Labial]

LEXICON Erg[Decl4]
<erg>:ади[Back]
<erg>:еди[NonBack]

LEXICON Erg[Decl5]
<erg>:ра[Back]
<erg>:ре[NonBack]

LEXICON ErgPlural
<pl><erg>:ари[Back]
<pl><erg>:ери[NonBack]

LEXICON Elative
<elative>:ай

LEXICON Directive
<directive>:ди

LEXICON NounInflEssive
<ad><essive>:в
<post><essive>:хъ
<sub><essive>:к
<in><essive>:а 
<super><essive>:л

LEXICON Plural
<pl>:ар[Back]
<pl>:ер[NonBack]
